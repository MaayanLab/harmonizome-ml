group = 'edu.mssm.pharm.maayanlab.Harmonizome-ML'
description = 'Harmonizome-ML'

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        } 
    }

    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
        classpath 'ru.vyarus:gradle-use-python-plugin:1.0.1'
    }
}


apply plugin: 'ru.vyarus.use-python'
apply plugin: 'com.moowork.node'

node {
  download = true
  nodeModulesDir = file(project.projectDir)
}

python {
   minPythonVersion = '3.5.0'
   scope = VIRTUALENV
}

task prepareNpm(type: NpmTask) {
    inputs.file("${project.projectDir}/package.json")
    outputs.dir("${project.projectDir}/node_modules")
    args = [ "install" ]
}

task buildProject(type: NpmTask) {
    inputs.file("${project.projectDir}/gruntfile.js")
    inputs.files fileTree(project.projectDir).include('**/*.js').include('**/*.less').exclude('node_modules/**').exclude('app/static/')
    outputs.files fileTree(project.projectDir).include('app/static/**').include('app/templates/*.html')
    dependsOn checkPython
    dependsOn prepareNpm
    args = [ "run", "build" ]
}
build.dependsOn buildProject

task runProject(type: NpmTask) {
    outputs.upToDateWhen { false }
    dependsOn buildProject
    args = [ "run", "start" ]
}
